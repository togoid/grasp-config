"""
--- endpoint ---
https://rdfportal.org/ebi/sparql

--- sparql ---
PREFIX : <https://github.com/dbcls/grasp/ns/>
PREFIX terms: <http://rdf.ebi.ac.uk/terms/ensembl/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?ensembl_transcript :iri ?ensembl_transcript .
  ?ensembl_transcript :id ?id .
  ?ensembl_transcript :label ?label .
  ?ensembl_transcript :transcript_flag ?transcript_flag .
}
WHERE {
  GRAPH <http://rdfportal.org/dataset/ensembl> {
    ?ensembl_transcript a terms:EnsemblTranscript ;
      dcterms:identifier ?id ;
      rdfs:label ?label .
    {{#if transcript_flag}}
    ?ensembl_transcript terms:has_transcript_flag / rdfs:label ?transcript_flag .
    # To obtain flags other than specified flags, ?filter_flag (= flags to be used for filtering) and ?transcript_flag (= all flags an entry has) must be distinguished.
    ?ensembl_transcript terms:has_transcript_flag / rdfs:label ?filter_flag .
    {{else}}
    OPTIONAL {
      ?ensembl_transcript terms:has_transcript_flag / rdfs:label ?transcript_flag .
    }
    {{/if}}

    {{#if iri}}VALUES ?ensembl_transcript { {{join " " (as-iriref iri)}} }{{/if}}
    {{#if id}}VALUES ?id { {{join " " (as-string id)}} }{{/if}}
    {{#if transcript_flag}}
      VALUES ?filter_flag {
        {{#each transcript_flag as |_flag|}}
          "{{_flag}}"
        {{/each}}
      }
    {{/if}}
  }
}
"""
type ensembl_transcript {
  iri: String!
  id: String!
  label: String!
  transcript_flag: [String]
}
